buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath 'com.github.dcendents:android-maven-plugin:1.1'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'android-maven'
project.version = '1.3.1'
android {
    compileSdkVersion 19
    buildToolsVersion '21.1.0'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 16
        versionCode 4
        versionName "1.3.1"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['src']
            assets.srcDirs = ['assets']
        }
    }
}
uploadArchives {
    repositories {
        mavenDeployer {
            repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
            pom.artifactId = "SPiD-SDK"
            pom.version = '1.3.1'
        }
    }
}
task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java
}
android.libraryVariants.all { variant ->

    task("generate${variant.name}Javadoc", type: Javadoc) {
        title = "$name $version API"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://developer.android.com/reference/packages.html");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
task generateJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', "${project.name}-${project.version}.jar")
}
task generateSourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
task generateJavadocsJar(dependsOn: generateJavadocs, type: Jar) {
    classifier = 'javadoc'
    from generateJavadocs.destinationDir
}
artifacts {
    archives generateSourceJar
    archives generateJavadocsJar
}
assemble.dependsOn(generateJar)
assemble.dependsOn(generateSourceJar)
assemble.dependsOn(generateJavadocsJar)

dependencies {
}